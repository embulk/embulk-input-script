plugins {
    id "java"
    id "maven-publish"
    id "checkstyle"
    id "org.embulk.embulk-plugins" version "0.3.0"
}

repositories {
    mavenCentral()
    jcenter()
}

group = "org.embulk"
version = "0.1.1"
description = "Loads records from Script."

sourceCompatibility = 1.8
targetCompatibility = 1.8

tasks.withType(JavaCompile) {
    options.compilerArgs << "-Xlint:deprecation" << "-Xlint:unchecked"
    options.encoding = "UTF-8"
}

dependencies {
    compileOnly "org.embulk:embulk-core:0.9.23"
    compile "org.yaml:snakeyaml:1.18"
    compile("com.opencsv:opencsv:4.5") {
        exclude group: "commons-collections", module: "commons-collections"
        exclude group: "org.apache.commons", module: "commons-lang3"
    }
}

embulkPlugin {
    mainClass = "org.embulk.input.script.ScriptInputPlugin"
    category = "input"
    type = "script"
}

publishing {
    publications {
        embulkPluginMaven(MavenPublication) {  // Publish it with "publishEmbulkPluginMavenPublicationToMavenRepository".
            from components.java  // Must be "components.java". The dependency modification works only for it.
        }
    }
}

gem {
    authors = [ "Sadayuki Furuhashi" ]
    email = [ "frsyuki@gmail.com" ]
    summary = "Script input plugin for Embulk"
    homepage = "https://github.com/embulk/embulk-input-script"
    licenses = [ "MIT" ]
}

gemPush {
    host = "https://rubygems.org"
}

checkstyle {
    configFile = file("${project.rootDir}/config/checkstyle/checkstyle.xml")
    toolVersion = '6.14.1'
}
checkstyleMain {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
checkstyleTest {
    configFile = file("${project.rootDir}/config/checkstyle/default.xml")
    ignoreFailures = true
}
task checkstyle(type: Checkstyle) {
    classpath = sourceSets.main.output + sourceSets.test.output
    source = sourceSets.main.allJava + sourceSets.test.allJava
}
